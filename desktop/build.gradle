dependencies {
    implementation 'junit:junit:4.13.1'
}

sourceSets.main.java.srcDirs = [ "src/" ]

project.ext.mainClassName = "com.gatdsen.DesktopLauncher"

import org.gradle.internal.os.OperatingSystem

task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    archiveVersion.set "1.0.1"
    archiveBaseName = "gaTDsen"
    destinationDirectory = file("$rootDir")
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

processResources.dependsOn ':pipeline:compileAssets'

dist.dependsOn classes

eclipse.project.name = appName + "-desktop"
